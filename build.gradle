plugins {
    id 'java'
}

group 'com.hiczp.spigot.cqbridge'
version '0.0.1'
description 'Create chat bridge between spigot server and CoolQ via CQHttp'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://jitpack.io' }
}

configurations {
    extraLibs
    implementation {
        extendsFrom(extraLibs)
    }
}

//spigot
dependencies {
    implementation 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT'
}

//cqbot
dependencies {
    //remove unnecessary package
    extraLibs('com.github.HyDevelop:PicqBotX:4.12.0.1019.PRE') {
        exclude group: 'org.projectlombok', module: 'lombok'
        exclude group: 'cn.hutool', module: 'hutool-all'
    }
    // https://mvnrepository.com/artifact/cn.hutool/hutool-core
    extraLibs group: 'cn.hutool', name: 'hutool-core', version: '5.2.3'
    // https://mvnrepository.com/artifact/cn.hutool/hutool-http
    extraLibs group: 'cn.hutool', name: 'hutool-http', version: '5.2.3'
    // https://mvnrepository.com/artifact/cn.hutool/hutool-json
    extraLibs group: 'cn.hutool', name: 'hutool-json', version: '5.2.3'
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
